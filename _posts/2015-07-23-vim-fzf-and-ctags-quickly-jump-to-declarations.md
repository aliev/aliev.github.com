---
layout: post
title: Vim, FZF and Ctags - quickly jump to declarations
tags: "fzf, ctags, vim"
category: en
published: true
---

I decided to refuse CtrlP in favor of FZF. FZF is more universal tool which can work outside the editor. I propose the solution, on the basis of FZF and ctags which allows to quickly pass on methods, imports, classes, etc. (thanks for [@junegunn](https://github.com/junegunn)). This solution has an advantage, it doesn't create the tags file and works only within the current buffer:

{% highlight vim %}
" ----------------------------------------------------------------------------
" BTags
" ----------------------------------------------------------------------------
function! s:align_lists(lists)
  let maxes = {}
  for list in a:lists
    let i = 0
    while i < len(list)
      let maxes[i] = max([get(maxes, i, 0), len(list[i])])
      let i += 1
    endwhile
  endfor
  for list in a:lists
    call map(list, "printf('%-'.maxes[v:key].'s', v:val)")
  endfor
  return a:lists
endfunction

function! s:btags_source()
  let lines = map(split(system(printf(
    \ 'ctags -f - --sort=no --excmd=pattern --language-force=%s %s',
    \ &filetype, expand('%:S'))), "\n"), 'split(v:val, "\t")')
  if v:shell_error
    throw 'failed to extract tags'
  endif
  return map(s:align_lists(lines), 'join(v:val, "\t")')
endfunction

function! s:btags_sink(line)
  execute split(a:line, "\t")[2]
endfunction

function! s:btags()
  try
    call fzf#run({'source':  s:btags_source(),
                 \'down':    '40%',
                 \'options': '+m -d "\t" --with-nth 1,4..',
                 \'sink':    function('s:btags_sink')})
  catch
    echohl WarningMsg
    echom v:exception
    echohl None
  endtry
endfunction

command! BTags call s:btags()

function! s:buflist()
  redir => ls
  silent ls
  redir END
  return split(ls, '\n')
endfunction

function! s:bufopen(e)
  execute 'buffer' matchstr(a:e, '^[ 0-9]*')
endfunction

nnoremap <silent> <Leader>b :call fzf#run({
\   'source':  reverse(<sid>buflist()),
\   'sink':    function('<sid>bufopen'),
\   'options': '+m',
\   'down':    len(<sid>buflist()) + 2
\ })<CR>
{% endhighlight %}


Here is how it looks in work:


![Vim and ctags with FZF quickly jump to declarations](/assets/article_images/2015-07-23-vim-fzf-and-ctags-quickly-jump-to-declarations/vim-ctags-fzf.png "Vim and ctags with FZF quickly jump to declarations")
